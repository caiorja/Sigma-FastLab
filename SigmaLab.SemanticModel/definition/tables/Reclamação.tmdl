table Reclamação
	lineageTag: d8685c20-cb14-4536-9e5f-2a09d23444c2

	measure 'Índice de Reclamações' =
			
			DIVIDE(
			    COUNTROWS(fato_reclamacao),
			    COUNTROWS(fato_exame),
			    0
			)*1000
		formatString: #,0.00
		lineageTag: 5067c023-b25f-4824-92a8-dacf570045cf

	measure Reclamações = COUNTROWS(fato_reclamacao)
		formatString: #,0
		lineageTag: fd653381-34f9-41ad-8a8c-06ca3a787cd5

	measure 'Índice de reclamacoes ano anterior' =
			
			CALCULATE([Índice de Reclamações], SAMEPERIODLASTYEAR(dim_tempo[data]))
		formatString: #,0.00
		lineageTag: 8829291d-f60b-4dff-911d-882d4ef1af78

	measure 'Índice de reclamacao variacao ano %' = ```
			
			 DIVIDE(
			        ([Índice de Reclamações]-[Índice de reclamacoes ano anterior]), [Índice de reclamacoes ano anterior],0)
			```
		formatString: 0.00%;-0.00%;0.00%
		lineageTag: ff988181-059a-4a7c-b3c1-5e9b5d901464

	measure 'Índice de reclamacoes dinamico variacao' = ```
			
			VAR VOL = DIVIDE(
			        ([Índice de Reclamações] - [Índice de reclamacoes ano anterior]), [Índice de reclamacoes ano anterior],0)
			
			VAR ICON = 
			        SWITCH(
			        TRUE(),
			            [Índice de reclamacao variacao ano %] >0,"⮝ ",
			            [Índice de reclamacao variacao ano %] <0,"⮟"
			        )
			        VAR 
			            RESULT = 
			                    IF(VOL = 0 , BLANK() , 
			                    CONCATENATE(ICON,CONCATENATE("",FORMAT(VOL,"0.00%;-0.00%;0.00%")))&" vs ano anterior")
			RETURN
			RESULT
			```
		lineageTag: 4b6ee8e5-11d6-4306-9d80-13cf1664fad3

	measure 'Falhas %' =
			
			DIVIDE(
			        [Atrasos],[Exame],0)
		formatString: 0.00%;-0.00%;0.00%
		lineageTag: b85a6f2c-fdcf-4503-8195-dde8ed51260b

	measure 'Falhas (%) Ano Anterior' = ```
			
			CALCULATE(
			    [Falhas %],
			    SAMEPERIODLASTYEAR(dim_tempo[data])
			)
			
			```
		formatString: 0.00%;-0.00%;0.00%
		lineageTag: 94115b4a-6348-4692-89a0-7214754bda84

	measure 'Falhas (%) Variação Ano %' = ```
			
			DIVIDE(
			    ([Falhas %] - [Falhas (%) Ano Anterior]),
			    [Falhas (%) Ano Anterior],
			    0
			)
			
			```
		formatString: 0.00%;-0.00%;0.00%
		lineageTag: 9d1e3f38-b272-419f-aecd-0b2e8ebc914e

	measure 'Falhas (%) Dinâmico' = ```
			
			VAR VARIACAO = [Falhas (%) Variação Ano %]
			VAR ICONE =
			    SWITCH(
			        TRUE(),
			        VARIACAO > 0, "⮝ ",
			        VARIACAO < 0, "⮟ ",
			        ""
			    )
			VAR RESULTADO =
			    IF(
			        VARIACAO = 0,
			        BLANK(),
			        ICONE & FORMAT(VARIACAO, "0.00%;-0.00%;0.00%") & " vs ano anterior"
			    )
			RETURN
			RESULTADO
			
			```
		lineageTag: 08367d2d-55d2-4602-94cd-df508686f779

	measure 'Correlação % Falhas × Reclamações' = ```
			
			VAR T0 =
			    SUMMARIZE (
			        dim_area_laboratorial,
			        dim_area_laboratorial[id_area],
			        "x", CALCULATE ( [Falhas %] ),
			        "y", CALCULATE ( [Índice de Reclamações] )
			    )
			VAR T =
			    FILTER ( T0, NOT ISBLANK ( [x] ) && NOT ISBLANK ( [y] ) )
			VAR n = COUNTROWS ( T )
			VAR avgX = AVERAGEX ( T, [x] )
			VAR avgY = AVERAGEX ( T, [y] )
			VAR covXY = AVERAGEX ( T, ( [x] - avgX ) * ( [y] - avgY ) )
			VAR sdX = SQRT ( AVERAGEX ( T, POWER ( [x] - avgX, 2 ) ) )
			VAR sdY = SQRT ( AVERAGEX ( T, POWER ( [y] - avgY, 2 ) ) )
			RETURN
			IF ( n < 2 || sdX = 0 || sdY = 0, BLANK (), DIVIDE ( covXY, sdX * sdY ) )
			
			```
		formatString: #,0.00
		lineageTag: d7b4f30a-adf9-4f9e-8ae5-3560761a8331

	measure 'Texto índice de reclamação' = "Reclamações para cada 1.000 exames processados"
		lineageTag: 6313f3bb-c6b8-4d93-b79f-8157349cded8

	partition Reclamação = m
		mode: import
		source =
				let
				    Fonte = Table.FromRows(Json.Document(Binary.Decompress(Binary.FromText("i44FAA==", BinaryEncoding.Base64), Compression.Deflate)), let _t = ((type nullable text) meta [Serialized.Text = true]) in type table [#"Coluna 1" = _t]),
				    #"Tipo Alterado" = Table.TransformColumnTypes(Fonte,{{"Coluna 1", type text}}),
				    #"Colunas Removidas" = Table.RemoveColumns(#"Tipo Alterado",{"Coluna 1"})
				in
				    #"Colunas Removidas"

	annotation PBI_ResultType = Table

